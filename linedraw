'Set 4 WS2812B displays with GPIO15 as input
neo.setup 15,128
'for a serpentine type neopixel board where 0,0, is bottom left
ymax = 8  ' this is the length of the colum in pixels 

'draw 200 random lines in a 16x8 neorgb matrix
m = millis
for i= 0 to 200 
cc = neo.rgb(rnd(30),rnd(30),rnd(30))   'get arandom color
drawline rnd(16),rnd(8),rnd(16),rnd(8),cc      ' draw a random line
neo.pixel 0,neo.getpixel(0)  ' this is a kludge to display all pixels
for t = 0 to 127     ' clear the display  - set all pixels to 0
neo.pixel t,0,0,0,1  ' but dont display anything, (last value set to 1)
next t
next i
print millis - m




end

'https://gist.github.com/msikma/16c9e4c2fe6577944e5f
sub drawline(qx1,qy1,qx2,qy2,qc)

local i,dx,dy,sdx,sdy,dxabs,dyabs,x,y,px,py,x1,x2,y1,y2,color
  x1 = qx1: y1 = qy1: x2 = qx2: y2 = qy2: color = qc

  dx=x2-x1     ' the horizontal distance of the line */
  dy=y2-y1      ' the vertical distance of the line */
  dxabs=abs(dx)
  dyabs=abs(dy)
  sdx=sgn(dx)
  sdy=sgn(dy)
  x=dyabs>>1
  y=dxabs>>1
  px=x1
  py=y1


  if (dxabs>=dyabs) then '  the line is more horizontal than vertical 
  
    plot px,py,color
    for i=0 to dxabs   
      y =y +dyabs
      if y>=dxabs then     
        y=y-dxabs:py=py+sdy
      end if
      px=px+sdx
      plot px,py,color 
    next i 
  else '/* the line is more vertical than horizontal */ 
    plot px,py,color
    for i=0 to dyabs    
      x=x+dxabs
      if x>=dyabs then      
        x=x-dyabs:px=px+sdx
      end if
      py=py+sdy
      plot px,py,color   
  next i
  end if
end sub

sub plot(qx,qy,qc)  ' set up for a serpentine display
if (qx mod 2 )=1 then  ' for a normal neomatrix type display with a raster laybout can get rid of this line
 neo.pixel qx*ymax+ymax-qy-1,qc,1    ' and this line
 else                  'and this line
 neo.pixel qx*ymax+qy,qc,1
end if     ' and this line
end sub

 

